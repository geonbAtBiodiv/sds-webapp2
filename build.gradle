buildscript {
    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath "com.bertramlabs.plugins:asset-pipeline-gradle:3.4.6"
    }
}

plugins {
    id "com.gorylenko.gradle-git-properties" version "2.4.1"
}

version "1.7.1"
group "au.org.ala"

apply plugin:"eclipse"
apply plugin:"idea"
apply plugin:"war"
apply plugin:"org.grails.grails-web"
apply plugin:"org.grails.grails-gsp"
apply plugin:"com.bertramlabs.asset-pipeline"
apply plugin:"maven-publish"

publishing {
    repositories {
        maven {
            name 'Nexus'
            url "https://nexus.ala.org.au/content/repositories/${project.version.endsWith('-SNAPSHOT') ? 'snapshots' : 'releases'}"
            credentials {
                username = System.getenv('TRAVIS_DEPLOY_USERNAME')
                password = System.getenv('TRAVIS_DEPLOY_PASSWORD')
            }
        }
    }
    publications {
        mavenJar(MavenPublication) {
            artifact bootWar
        }
    }
}

bootWar {
    launchScript()
}

repositories {
    mavenLocal()
    maven { url "https://repo.grails.org/grails/core" }
    maven { url "https://nexus.ala.org.au/content/groups/public/" }
}

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

dependencies {
    developmentOnly("org.springframework.boot:spring-boot-devtools")
    implementation "org.springframework.boot:spring-boot-autoconfigure"
    implementation "org.grails:grails-core"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-tomcat"
    implementation "org.grails:grails-dependencies"
    implementation "org.grails:grails-web-boot"
    implementation "org.grails.plugins:cache"
    console "org.grails:grails-console"
    profile "org.grails.profiles:web"
    runtimeOnly "com.bertramlabs.plugins:asset-pipeline-grails:3.4.6"
    runtimeOnly "org.hibernate:hibernate-validator:5.4.2.Final"


    testImplementation "org.grails:grails-gorm-testing-support"
    testImplementation "org.grails:grails-web-testing-support"
    //runtimeOnly "com.h2database:h2"
    testImplementation "org.grails.plugins:geb"
    testImplementation "org.seleniumhq.selenium:selenium-htmlunit-driver:2.47.1"
    testImplementation "net.sourceforge.htmlunit:htmlunit:2.18"

    implementation(group: 'au.org.ala', name: 'sds', version: '1.4.8') {
        exclude group: 'org.springframework', module: 'spring-jdbc'
        exclude group: 'javax.servlet', module: 'servlet-api'
        exclude group: 'org.apache.bval', module: 'org.apache.bval.bundle'
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    }
    implementation 'dk.glasius:external-config:3.1.1'
    runtimeOnly "org.grails.plugins:ala-bootstrap3:4.4.0"
    implementation(group: "org.grails.plugins", name: "ala-auth", version:"$alaSecurityLibsVersion") {
        exclude group: 'javax.servlet', module: 'servlet-api'
    }
    implementation 'org.grails.plugins:ala-admin-plugin:2.3.0'
    implementation 'au.org.ala.plugins:openapi:1.1.0'
}

bootRun {
    ignoreExitValue true
    jvmArgs(
            '-Dspring.output.ansi.enabled=always',
            '-noverify',
            '-XX:TieredStopAtLevel=1',
            '-Xmx1024m')
    sourceResources sourceSets.main
    String springProfilesActive = 'spring.profiles.active'
    systemProperty springProfilesActive, System.getProperty(springProfilesActive)
}


assets {
    minifyJs = true
    minifyCss = true
}
